// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Preset x input
    Mux16(a=x, b=false, sel=zx, out=xInput);
    Not16(in=xInput, out=notXInput);
    Mux16(a=xInput, b=notXInput, sel=nx, out=xOperand);

    // Preset y input
    Mux16(a=y, b=false, sel=zy, out=yInput);
    Not16(in=yInput, out=notYInput);
    Mux16(a=yInput, b=notYInput, sel=ny, out=yOperand);

    // Apply function
    Add16(a=xOperand, b=yOperand, out=xySum);
    And16(a=xOperand, b=yOperand, out=xyAnd);
    Mux16(a=xyAnd, b=xySum, sel=f, out=fxy);

    // Apply negation
    Not16(in=fxy, out=notfxy);
    Mux16(a=fxy, b=notfxy, sel=no, out=out);

    // Set ng
    Mux16(a=fxy, b=notfxy, sel=no, out[15]=ng);

    // Set zr
    Mux16(a=fxy, b=notfxy, sel=no, out[0..7]=lowBits);
    Or8Way(in=lowBits, out=areLowBitsNotZero);
    Mux16(a=fxy, b=notfxy, sel=no, out[8..15]=highBits);
    Or8Way(in=highBits, out=areHighBitsNotZero);
    Or(a=areLowBitsNotZero, b=areHighBitsNotZero, out=isNotZero);
    Not(in=isNotZero, out=zr);
}